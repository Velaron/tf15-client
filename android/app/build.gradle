import java.time.LocalDateTime
import java.time.Month
import java.time.temporal.ChronoUnit

apply plugin: 'com.android.application'

android {
    compileSdk 33
    ndkVersion '25.2.9519653'
    buildToolsVersion '33.0.2'
    namespace 'su.xash.tf15client'

    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
    }

    defaultConfig {
        applicationId 'su.xash'
        applicationIdSuffix 'tf15client'
        versionName '0.99'
        versionCode getBuildNum()
        minSdkVersion 16
        targetSdk 33

        externalNativeBuild {
            cmake {
                version '3.22.1'
            }
        }
    }

    externalNativeBuild {
        cmake {
            version '3.22.1'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    externalNativeBuild {
        cmake {
            path file('../../CMakeLists.txt')
        }
    }

    lint {
        abortOnError false
    }

    aaptOptions {
        noCompress ''
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
}

static def getBuildNum() {
    LocalDateTime now = LocalDateTime.now()
    LocalDateTime releaseDate = LocalDateTime.of(2018, Month.DECEMBER, 7, 0, 0, 0)
    int qBuildNum = releaseDate.until(now, ChronoUnit.DAYS)
    int minuteOfDay = now.getHour() * 60 + now.getMinute()
    return qBuildNum * 10000 + minuteOfDay
}